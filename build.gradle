/*
* Copyright (c) 2016 Lite Solutions
*
*  This code is licensed under the Apache Software License version 2.
*  For more information, see the LICENSE file at the root of this package.
*
*  Should you not have the source code available, and the file above is
*  unavailable, you can obtain a copy of the license here:
*
*  https://www.apache.org/licenses/LICENSE-2.0.txt
*
*/
plugins {
    id 'java-library'
    id 'maven-publish'
}

group = "org.litesolutions"
version = "0.6.2"
description = "Grappa integration for Sonar's SSLR"

def javaVersion = JavaVersion.VERSION_1_8
sourceCompatibility = javaVersion
targetCompatibility = javaVersion // defaults to sourceCompatibility

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.codehaus.sonar.sslr-squid-bridge:sslr-squid-bridge:2.6')
    implementation('com.github.fge:grappa:2.0.4:all') {
        exclude group: "com.google.guava", module: "guava"
    }
    testImplementation("org.codehaus.sonar.sslr:sslr-testing-harness:1.20") {
        exclude group: "junit", module: "junit"
        exclude group: "org.easytesting", module: "fest-assert"
    }
}

project.ext {
    gitrwscm = sprintf("git@github.com:litesolutions/%s", name)
    gitroscm = sprintf("https://github.com/litesolutions/%s.git", name)
    projectURL = sprintf("https://github.com/litesolutions/%s", name)
    sonatypeStaging = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    sonatypeSnapshots = "https://oss.sonatype.org/content/repositories/snapshots/"
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "${project.getGroup()}"
            artifactId = "${project.getName()}"
            version = "${project.getVersion()}"
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom.withXml {
                asNode().dependencies.'*'.each() {
                    it.scope*.value = 'compile'
                }
            }
            pom {
                description = "${project.description}"
                url = "${projectURL}"
                packaging = "jar"

                scm {
                    url = "${gitrwscm}"
                    connection = "${gitrwscm}"
                    developerConnection = "${gitroscm}"
                }

                licenses {
                    license {
                        name = "Apache Software License, version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0"
                    }
                }

                developers {
                    developer {
                        id = "fge"
                        name = "Francis Galiegue"
                        email = "fgaliegue@gmail.com"
                    }
                    developer {
                        id = "daimor"
                        name = "Dmitry Maslennikov"
                        email = "mrdaimor@gmail.com"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/octocat/hello-world"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}